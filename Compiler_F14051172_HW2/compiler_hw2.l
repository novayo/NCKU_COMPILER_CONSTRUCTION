/* Definition section */
%{
#include "y.tab.h"	/* header file generated by bison */
#include <stdio.h>
#define BUF_SIZE 256
extern YYSTYPE yylval; //YYSTYPE
char buf[BUF_SIZE];

/* This macro is used to record code line */
#define CONCAT { strncat(buf, yytext, BUF_SIZE - strlen(buf) - 1); }
%}

/* Define regular expression label */
digit		[0-9]
letter		[a-zA-Z]
integer     {digit}+
float       {digit}+"."{digit}+
id			{letter}+({letter}|{digit})*

%option yylineno

%x COMMENT STRING_STATE

/* Rules section */
%%

 /* Arithmetic */
"+"     { CONCAT; return '+'; }
"-" 	{ CONCAT; return '-'; }
"*" 	{ CONCAT; return '*'; }
"/" 	{ CONCAT; return '/'; }
"%"     { CONCAT; return '%'; }
"++"    { CONCAT; return INC_OP; }
"--"    { CONCAT; return DEC_OP; }

 /* Relational */
">"		{ CONCAT; return '>'; }
"<"		{ CONCAT; return '<'; }
">="	{ CONCAT; return GE_OP; }
"<="	{ CONCAT; return LE_OP; }
"=="	{ CONCAT; return EQ_OP; }
"!="	{ CONCAT; return NE_OP; }

 /* Assignment */
"="		{ CONCAT; return '='; }
"+="	{ CONCAT; return ADD_ASSIGN; }
"-="	{ CONCAT; return SUB_ASSIGN; }
"*="	{ CONCAT; return MUL_ASSIGN; }
"/="	{ CONCAT; return DIV_ASSIGN; }
"%="	{ CONCAT; return MOD_ASSIGN; }

 /* Logical */
"&&"	{ CONCAT; return AND_OP; }
"||"	{ CONCAT; return OR_OP; }
"!"		{ CONCAT; return '!'; }

 /* Delimiters */
"("		{ CONCAT; return '('; }
")"		{ CONCAT; return ')'; }
"{"		{ CONCAT; return '{'; }
"}"		{ CONCAT; return '}'; }
"["		{ CONCAT; return '['; }
"]"		{ CONCAT; return ']'; }
","		{ CONCAT; return ','; }

 /* Print Keywords */	
"print" { CONCAT; return PRINT;}

 /* Condition and Loop Keywords */
"if"		{ CONCAT; return IF; }
"else"		{ CONCAT; return ELSE; }
"for"		{ CONCAT; return FOR; }
"while"		{ CONCAT; return WHILE; }

 /* Declaration Keywords */
"void"		{ CONCAT; return VOID; }
"int"		{ CONCAT; return INT; }
"float"  	{ CONCAT; return FLOAT; }
"string"  	{ CONCAT; return STRING; }
"bool"  	{ CONCAT; return BOOL; }

 /* boolean Keywords */
"true"      { CONCAT; return TRUE; }
"false"     { CONCAT; return FALSE; }

"return"    { CONCAT; return RETURN; }

 /* String Constant */
\"				        { CONCAT; BEGIN STRING_STATE; return '"'; }
<STRING_STATE>\"		{ CONCAT; BEGIN INITIAL; return '"'; }
<STRING_STATE>[^"]*		{ CONCAT; return STR_CONST; }

 /* Number Constant */
{integer}	{ CONCAT; return I_CONST; }
{float}	    { CONCAT; return F_CONST; }

 /* C type Comment */
"/*"				{ CONCAT; BEGIN COMMENT; }
<COMMENT>[^*\n]+	{ CONCAT; }
<COMMENT>\n			{ CONCAT; printf("%d: %s", yylineno, buf); strcpy(buf, ""); }
<COMMENT>"*"		{ CONCAT; }
<COMMENT>"*/"		{ CONCAT; BEGIN INITIAL; }

 /* C++ type Comment */
\/\/.*	{ CONCAT; }

 /* Variable ID */
 {id}       { CONCAT; yylval.string = yytext; return IDENTIFIER; }

 /* others */

[;]         { CONCAT; return ';'; }
[\n]        { CONCAT; if (strcmp("}\n", buf)) printf("%d: %s", yylineno, buf); strcpy(buf, ""); }
[ \t]		{ CONCAT; } /* Ignore */
[^ \t\n] 	{ CONCAT; } /* Ignore other charactor sets */
<<EOF>>     { return 0; }

%%

int yywrap()
{
    return 1;
}
